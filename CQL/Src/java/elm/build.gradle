apply plugin: 'java'
apply plugin: 'idea'

group = 'org.cqframework'
version = '0.1'
sourceCompatibility = '1.7'

repositories {
    mavenCentral()
}

configurations {
    xjc
}

dependencies {
    xjc group: 'com.sun.xml.bind', name: 'jaxb-xjc', version: '2.2.7-b41'
    xjc group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.2.7-b41'
}

ext.xjc = [
    schema:  "${projectDir}/../../cql-lm/schema/elm/library.xsd",
    destDir: 'src/generated/java'
]

task generateSources << {
    file(xjc.destDir).mkdirs()

    ant.taskdef(name: 'xjc', classname: 'com.sun.tools.xjc.XJCTask', classpath: configurations.xjc.asPath)
    ant.xjc(destdir: xjc.destDir, schema: xjc.schema)
}

compileJava.dependsOn generateSources
sourceSets.main.java.srcDirs += xjc.destDir

clean {
    delete xjc.destDir
}