buildscript {
    repositories {
        maven {
            url 'http://dl.bintray.com/content/djmijares/gradle-plugins'
        }
        mavenCentral()
    }

    dependencies {
        classpath 'org.gradle.jacobo.plugins:gradle-jaxb-plugin:1.3.1'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'jaxb'

group = 'org.cqframework'
version = '0.1'
sourceCompatibility = '1.7'

repositories {
    mavenCentral()
}

dependencies {
    jaxb 'com.sun.xml.bind:jaxb-xjc:2.2.7-b41' //jaxws 2.2.6 uses jaxb 2.2.5, but can't dL 2.2.5 from maven the pom is off TODO
    jaxb 'com.sun.xml.bind:jaxb-impl:2.2.7-b41'
    jaxb 'javax.xml.bind:jaxb-api:2.2.7'
}

task copySchema(type: Copy) {
    from("${projectDir}/../../cql-lm/schema")
    into('schema')
    include('**/*')
}

jaxb {
    xsdDir            = "elm/schema/elm"
    episodesDir       = "elm/schema/episodes"
    bindingsDir       = "elm/schema/bindings"
    bindings          = []
    xjc {
        destinationDir     = "src/generated/java"
        producesDir        = "src/generated/java"
        extension          = 'true'
        removeOldOutput    = 'yes'
        header             = true
    }
}

xjc.dependsOn copySchema
compileJava.dependsOn xjc
sourceSets.main.java.srcDirs += jaxb.xjc.destinationDir

clean {
    delete jaxb.xjc.destinationDir
    delete 'schema'
}